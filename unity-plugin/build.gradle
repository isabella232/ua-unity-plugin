buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
    }
}

repositories {
    jcenter()
    google()
}

group = 'com.urbanairship.android'
version = airshipProperties.version
description = "Urban Airship Android Unity Plugin"

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion airshipProperties.androidCompileSdkVersion.toInteger()
    buildToolsVersion airshipProperties.androidBuildToolsVersion

    defaultConfig {
        minSdkVersion airshipProperties.androidMinSdkVersion.toInteger()
        targetSdkVersion airshipProperties.androidTargetSdkVersion.toInteger()
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    provided files('libs/unity-classes.jar')
    compile "com.android.support:support-v4:${airshipProperties.androidSupportLibVersion}"
    compile "com.android.support:cardview-v7:${airshipProperties.androidSupportLibVersion}"
    compile "com.android.support:support-annotations:${airshipProperties.androidSupportLibVersion}"
    compile "com.google.android.gms:play-services-gcm:${airshipProperties.androidPlayServicesVersion}"
    compile "com.urbanairship.android:urbanairship-sdk:${airshipProperties.androidAirshipVersion}"
}

task copyUnityClassesJar(type: Copy) {
  from file(unityClassesJar).getParent()
  include file(unityClassesJar).getName()
  rename ("classes.jar", "unity-classes.jar")
  into 'libs'
}

preBuild.dependsOn copyUnityClassesJar

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId project.group
            artifactId project.name

            name project.name
            description project.description
            url 'http://www.urbanairship.com'
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']

    pkg {
        userOrg = 'urbanairship'
        name = project.name
        desc = project.description
        websiteUrl = 'http://www.urbanairship.com'

        version {
            repo = 'android'
            publish = true
            desc = project.description
            name = project.version

            released = new Date()
        }
    }
}
