buildscript {
    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
    }
}

repositories {
    jcenter()
    google()
}

group = 'com.urbanairship.android'
version = airshipProperties.version
description = "Urban Airship Android Unity Plugin"

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'


android {
    compileSdkVersion airshipProperties.androidCompileSdkVersion.toInteger()

    defaultConfig {
        minSdkVersion airshipProperties.androidMinSdkVersion.toInteger()
        targetSdkVersion airshipProperties.androidTargetSdkVersion.toInteger()
    }
}

dependencies {
    compileOnly files('libs/unity-classes.jar')
    implementation "com.android.support:support-v4:${airshipProperties.androidSupportLibVersion}"
    implementation "com.android.support:support-annotations:${airshipProperties.androidSupportLibVersion}"
    implementation "com.urbanairship.android:urbanairship-fcm:${airshipProperties.androidAirshipVersion}"
}

task copyUnityClassesJar(type: Copy) {
  from file(unityClassesJar).getParent()
  include file(unityClassesJar).getName()
  rename ("classes.jar", "unity-classes.jar")
  into 'libs'
}

preBuild.dependsOn copyUnityClassesJar

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }

    developers {
        developer {
            name 'Urban Airship'
        }
    }

    scm {
        connection 'https://github.com/urbanairship/unity-plugin.git'
        url 'https://github.com/urbanairship/unity-plugin'
    }
}

publish.dependsOn "assembleRelease"

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/" + project.name + "-release.aar")
            groupId project.group
            artifactId project.name

            pom.withXml {
                def root = asNode()

                def dependenciesNode = root.appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }

                root.appendNode('description', project.description)
                root.appendNode('name', project.name)
                root.appendNode('url', 'https://github.com/urbanairship/unity-plugin')
                root.children().last() + pomConfig
            }
            repositories {
                maven {
                    url "$buildDir/m2repository"
                }
            }
        }
    }
}
